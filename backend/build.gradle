plugins {
  id 'org.springframework.boot' version '3.3.3'
  id 'io.spring.dependency-management' version '1.1.6'
  id 'org.jetbrains.kotlin.jvm' version '2.0.20'
  id 'org.jetbrains.kotlin.plugin.spring' version '2.0.20'
  id 'org.jetbrains.kotlin.plugin.noarg' version "2.0.20"
  id 'org.jetbrains.kotlin.kapt' version "2.0.20"
}

group = 'xyz.poeschl.roborush'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_21

repositories {
  mavenCentral()
}

ext {
  set('testcontainersVersion', "1.19.3")
  set('jackson.version', "2.17.0")
}

dependencies {
  implementation 'org.flywaydb:flyway-core'
  implementation 'org.flywaydb:flyway-database-postgresql'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-websocket'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.security:spring-security-messaging'
  implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
  implementation 'io.jsonwebtoken:jjwt-impl:0.12.6'
  implementation 'io.jsonwebtoken:jjwt-jackson:0.12.6'
  implementation 'org.postgresql:postgresql'
  implementation 'net.karneim:pojobuilder:4.3.0:annotations'

  kapt 'net.karneim:pojobuilder:4.3.0'

  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude module: 'mockito-core'
  }
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:postgresql'
  testImplementation 'io.mockk:mockk:1.13.12'
  testImplementation 'de.pixel.mcc:MigrationChange-Checker:1.2.1'
}

dependencyManagement {
  imports {
    mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
  }
}

noArg {
  annotation("jakarta.persistence.Entity")
}

kotlin {
  jvmToolchain {
    languageVersion.set(JavaLanguageVersion.of("21"))
  }
}

import org.gradle.api.tasks.testing.logging.TestLogEvent

tasks.withType(Test).configureEach {
  useJUnitPlatform()

  testLogging {
    events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED
  }
}
